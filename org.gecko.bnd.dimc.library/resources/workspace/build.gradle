plugins {
  id "org.sonarqube" version "3.1.1"
  id 'jacoco'
  id 'java'
}

// It is always good to give gradle the possibility to have a repository to download dpendencies
repositories {
    mavenCentral()
}

// Allow gradle to enable JUnit support in submodules
subprojects {
   apply plugin : 'java'
   test.useJUnitPlatform()
}

// Sonar setup
sonarqube {
  properties {
    property "sonar.projectName", "€€project-symbolic-name€€"
    property "sonar.projectKey", "geckoprojects-org_€€project-symbolic-name€€"
    property "sonar.organization", "geckoprojects-org"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.coverage.jacoco.xmlReportPaths", "${rootDir}/build/reports/jacoco/codeCoverageReport/codeCoverageReport.xml"
    //property "sonar.exclusions", "org.gecko.project/**/*.java, org.gecko.other.project/**/*.java"
  }
}

tasks.named('wrapper') {
  jarFile = rootProject.file('.gradle-wrapper/gradle-wrapper.jar')
}


// Disable bnd plugin built-in task 'testOSGi' and register custom task for testing
gradle.startParameter.excludedTaskNames += "testOSGi"

// Enable itest task instead for using OSGi Tests and JUnit5
tasks.register('itest'){
  subprojects.each { sp ->
    sp.tasks.each { t -> 
      if(t.name ==~ "resolve\\.(test|export)(.*)?"){
        dependsOn t
      }
    }
  }
  subprojects.each { sp ->
    sp.tasks.each { t -> 
      if(t.name ==~ "testrun\\.test(.*)?"){
        dependsOn t
      }
    }
  }
  subprojects.each { sp ->
    sp.tasks.each { t -> 
      if(t.name ==~ "export\\.export(.*)?"){
        dependsOn t
      }
    }
  }
}

task codeCoverageReport(type: JacocoReport) {
    dependsOn itest
    // Gather execution data from all subprojects
    executionData fileTree(project.rootDir.absolutePath).include("**/**/jacoco.exec")

    // Add all relevant sourcesets from the subprojects
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
}

tasks.named("sonarqube") {
    dependsOn(codeCoverageReport)
}

